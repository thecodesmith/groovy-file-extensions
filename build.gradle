plugins {
    id 'groovy'
    id 'org.kordamp.gradle.groovy-project' version '0.38.0'
    id 'org.kordamp.gradle.bintray'        version '0.38.0'
    id 'org.kordamp.gradle.guide'          version '0.38.0'
}

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = 'groovy-file-extensions'
        vendor      = 'thecodesmith'
        description = 'Useful enhancements for the dealing with files'

        links {
            website      = 'https://github.com/thecodesmith/groovy-file-extensions'
            issueTracker = 'https://github.com/thecodesmith/groovy-file-extensions/issues'
            scm          = 'https://github.com/thecodesmith/groovy-file-extensions.git'
        }

        people {
            person {
                id    = 'thecodesmith'
                name  = 'Brian Stewart'
                roles = ['developer']
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'MIT'
            }
        }
        enabled = false
    }

    bintray {
        userOrg    = 'thecodesmith'
        name       = 'groovy-file-extensions'
        repo       = 'maven'
        githubRepo = 'thecodesmith/groovy-file-extensions'
        credentials {
            username = 'thecodesmith'
            password = project.findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY') ?: 'unknown'
        }
    }

    docs {
        javadoc {
            enabled = false
        }

        groovydoc {
            enabled = true
            replaceJavadoc = true
        }
    }
}


repositories {
    jcenter()
}

dependencies {
    implementation("org.codehaus.groovy:groovy-all:2.5.12")
    implementation("com.bloidonia:groovy-common-extensions:0.6.0")

    implementation("org.slf4j:slf4j-api:1.7.16")
    testImplementation("ch.qos.logback:logback-classic:1.1.5")

    testImplementation("org.spockframework:spock-core:1.3-groovy-2.5") {
        exclude(group: "org.codehaus.groovy")
    }
}
